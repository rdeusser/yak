# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is dual-licensed under either the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree or the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree. You may select, at your option, one of the
# above-listed licenses.

load(":rules.bzl", "BANNED_DEP_PATHS", "LATE_BINDING_ONLY_CRATES", "TOP_LEVEL_ONLY_CRATES")

def _check_client_to_re_path(ctx: AnalysisContext):
    path = ctx.attrs.client_to_re_path
    if len(path) != 0:
        m = "yak client binary may not have a dependency on `//remote_execution/`!"
        m += "\nDependency path:"
        m += "".join(["\n" + str(t) for t in path])
        fail(m)

def _outside_app_yak(targets):
    # The library and binaries of `app/yak` share the dependencies of its `Cargo.toml`.
    return filter(lambda t: not str(t.label).startswith("root//app/yak:"), targets)

def _check_late_binding_only(ctx: AnalysisContext):
    for all_paths in ctx.attrs.late_binding_only_paths:
        all_paths = list(all_paths)
        target = all_paths.pop()
        remainder = _outside_app_yak(all_paths)
        if len(remainder) != 0:
            m = "Late-binding-only crate `" + str(target.label) + "` may not be depended on by:"
            m += "".join(["\n" + str(p.label) for p in remainder])
            fail(m)

def _check_top_level_only(ctx: AnalysisContext):
    for all_paths in ctx.attrs.top_level_only_paths:
        all_paths = list(all_paths)
        target = all_paths.pop()
        remainder = _outside_app_yak(all_paths)

        if len(remainder) != 0:
            m = "Top-level-only crate `" + str(target.label) + "` may not be depended on by:"
            m += "".join(["\n" + str(p.label) for p in remainder])
            fail(m)

def _check_banned_dep_paths(ctx: AnalysisContext):
    for path in ctx.attrs.banned_dep_paths:
        if len(path) > 0:
            a = path[0].label
            b = path[-1].label
            m = str(a) + " may not depend on " + str(b) + "! Path:"
            m += "".join(["\n" + str(p.label) for p in path])
            fail(m)

def _impl(ctx: AnalysisContext):
    _check_client_to_re_path(ctx)
    _check_late_binding_only(ctx)
    _check_top_level_only(ctx)
    _check_banned_dep_paths(ctx)
    return [DefaultInfo()]

_test_yak_dep_graph = rule(
    impl = _impl,
    attrs = {
        "banned_dep_paths": attrs.list(attrs.query()),
        "client_to_re_path": attrs.query(),
        "late_binding_only_paths": attrs.list(attrs.query()),
        "top_level_only_paths": attrs.list(attrs.query()),
    },
)

_CLIENT_BIN = "//app/yak:yak_client-bin"

_YAK_BIN = "//app/yak:yak"

_RE_CLIENT_TARGET = "//remote_execution/re_grpc:remote_execution"

_CLIENT_TO_RE = "somepath({}, filter(root//remote_execution/, deps({})) + {})".format(_CLIENT_BIN, _CLIENT_BIN, _RE_CLIENT_TARGET)

def test_yak_dep_graph(name):
    banned_dep_paths = []
    for a, b in BANNED_DEP_PATHS:
        if a > b:
            m = "`BANNED_DEP_PATHS` entries must be sorted:\n"
            m += "    " + str(a) + "\n"
            m += "  > " + str(b)
            fail(m)

        banned_dep_paths.append("somepath({}, {})".format(a, b))
        banned_dep_paths.append("somepath({}, {})".format(b, a))

    _test_yak_dep_graph(
        name = name,
        banned_dep_paths = banned_dep_paths,
        client_to_re_path = _CLIENT_TO_RE,
        late_binding_only_paths = ["allpaths({}, {})".format(_YAK_BIN, c) for c in LATE_BINDING_ONLY_CRATES],
        top_level_only_paths = ["allpaths({}, {})".format(_YAK_BIN, c) for c in TOP_LEVEL_ONLY_CRATES],
    )
